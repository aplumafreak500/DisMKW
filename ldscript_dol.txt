OUTPUT_ARCH(powerpc)

Text0Size = Text0End - Text0Start;

SECTIONS
{
	. = 0x80000000;
	DolphinGlobalsLow :
	ALIGN(4) {
		DolphinGlobals.o(.bss);
	}
	/* Gecko */
	.text3 :
	ALIGN(4) {
		text3.o(.text);
	}
	.data6 :
	ALIGN(4) {
		data9.o(.data);
	}
	. = 0x80003000;
	DolphinGlobalsHigh :
	ALIGN(4) {
		DolphinGlobals2.o(.bss);
	}
	. += 0x48; /* Section offsets */
	dol_header0 :
	ALIGN(4) {
		dolheader.o(.text1);
	}
	. += 0x0; /* Needed to match */
	.text0 :
	ALIGN(4) {
		Text0Start = .;
		TRK_Exception.o(.text);
		memset.o(.text);
		CheckGCPad4.o(.text);
		crt.o(.text);
		crt.o(.data);
		Text0End = .;
	}
	.data0 :
	ALIGN(4) {
		data0.o(.data);
		data1.o(.data);
	}
	.text1 :
	ALIGN(4) {
		text1.o(.text);
	}
	.data1 :
	ALIGN(4) {
		data2.o(.data);
		data3.o(.data);
	}
	. += 0x20; /* gap */
	.data2 :
	ALIGN(4) {
		data4.o(.data);
		data5.o(.data);
	}
	bss0Start = .;
	.bss0 :
	ALIGN(4) {
	}=0
	/* CTGP-Code is loaded here */
	. = 0x802a5000;
	.text2 :
	ALIGN(4) {
		text2.o(.text);
	}
	. = 0x802a8000;
	.data5 :
	ALIGN(4) {
		data8.o(.data);
	}
	.bss1 :
	ALIGN(4) {
	} = 0
	. = 0x80380870;
	bss0End = .;
	bss0Size = bss0End - bss0Start;
	. += 0x10; /* gap */
	/* D6 and D7 overlap .bss, maybe default heap? */
	.data3 :
	ALIGN(4) {
		data6.o(.data);
	}
	bss1Start = .;
	.bss2 :
	ALIGN(4) {
		. = 0xfcc;
	}=0
	bss1End = .;
	bss1Size = bss1End - bss1Start;
	. += 0x14; /* gap */
	.data4 :
	ALIGN(4) {
		data7.o(.data);
	}
	bss2Start = .;
	.bss3 :
	ALIGN(4) {
		. = 0x3c;
	}=0
	bss2End = .;
	bss2Size = bss2End - bss2Start;
	DolMemEnd = .;
	bssTotalSize = DolMemEnd - bss0Start; /* main.dol header */

    /* Discard everything not specifically mentioned above. */
    /DISCARD/ :
    {
        *(*);
    }
}

