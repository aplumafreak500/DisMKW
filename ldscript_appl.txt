OUTPUT_ARCH(powerpc)
ENTRY(apl_entrypoint)
Section0Size = init_data_end - init_text_start;
Section1Size = loader_end - loader_start;
init_text = ADDR(.header) + SIZEOF(.header);
init_data = init_text + (init_text_end - init_text_start);
loader = init_data + (init_data_end - init_data_start);
SECTIONS {
	.globalsLow 0x80000000 (NOLOAD):
	ALIGN(4) {
		../global/DolphinGlobals.o(.bss);
	} = 0
	.globalsHigh 0x80003000 (NOLOAD):
	ALIGN(4) {
		../global/DolphinGlobals2.o(.bss);
	} = 0
	.header :
	ALIGN(4) {
		header.o(.data);
	}
	.text 0x81200000:
	AT(init_text)
	ALIGN(4) {
		init_text_start = .;
		text.o(.text);
		init_text_end = .;
	} 
	.data :
	AT(init_data)
	ALIGN(4) {
		init_data_start = .;
		data.o(.data);
		init_data_end = .;
	}
	.loader : /* TODO: Has its own memory map; etc. */
	AT(loader)
	ALIGN(4) {
		loader_start = .;
		loader.dol.o(.text);
		loader_end = .;
	} 
	.bss (NOLOAD) : /* Does apploader.img really use a .bss portion? */
	ALIGN(4) {
	} = 0
	/* Discard everything not specifically mentioned above. */
	/DISCARD/ : {
		*(*);
	}
}
